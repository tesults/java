group 'com.tesults'
version '1.0.3'

apply plugin : "java"
apply plugin : "java-library"
apply plugin: "maven-publish"
apply plugin: "signing"

sourceCompatibility = 1.8

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    implementation 'javax.xml.bind:jaxb-api:2.3.0'
    implementation 'org.glassfish.jaxb:jaxb-runtime:2.3.1'
    implementation 'com.amazonaws:aws-java-sdk-s3'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.9.5'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.5'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.5'
    implementation 'org.json:json:20171018'
    implementation 'commons-io:commons-io:2.6'
    implementation platform('com.amazonaws:aws-java-sdk-bom:1.11.228')
}

java {
    withJavadocJar()
    withSourcesJar()
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'tesults'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'tesults'
                description = 'Tesults Java API Library'
                url = 'https://www.tesults.com'
                licenses {
                    license {
                        name = "MIT"
                        url = "https://opensource.org/licenses/MIT"
                        distribution = "repo"
                    }
                }
                developers {
                    developer {
                        id = "ajeetd"
                        name = "Ajeet Dhaliwal"
                        email = "help@tesults.com"
                    }
                }

                scm {
                    url = "https://github.com/tesults/java.git"
                }
            }
        }
    }
}

signing {
    sign publishing.publications.mavenJava
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}